# TEMPLATE MASTER BY MYSH (git.hugmouse.com)
~version: "2.0"
?path: /.+

# $(NBSP) FIXER
@replace("^\\s+$", ""): //text()

# twitter-tweet
<iframe>: //twitter-widget[starts-with(@data-tweet-id,"https://twitter.com/")]
@set_attr(src,./@data-tweet-id): $@
@replace:

# PIC RULES
# By @housemusicforhomelessepeople
<pic>: //img[ancestor::td] 

@set_attr("href",./img): //a[./img]/@href

@after_el("./.."): //p/iframe
@after_el("./.."): //a/img
<div>: //p/img
@after_el("./.."): //p/related

# STYLING (add it if you want)
# <aside>: //*[@style="text-align: center;"]
  
# REPLACING srcset WITH src 
# NOTE THAT IV DO NOT SUPPORT srcset!
# SAFE IF IV STARTS SUPPORTING srcset
# By @housemusicforhomelessepeople
$setsrc: $body//img[@srcset]
@set_attr(src_set, @srcset): 
@replace("http.+w, ", ""): $setsrc/@src_set
@replace("(jpe?g|png|gif).+w", "$1"): $setsrc/@src_set
@set_attr(src, @src_set): $setsrc

# YOUTUBE OBJECT FIXER
# By @housemusicforhomelessepeople
@if("//object[contains(@data, \"youtube\")]") {
  @before(<iframe>): //object[contains(@data, \"youtube\")]
  @replace("http://www.youtube.com/v/","https://www.youtube.com/embed/"): //object[contains(@data, "youtube")]/@data
  @set_attr(src, "./next-sibling::object[contains(@data, \"youtube\")]/@data"): //iframe[next-sibling::object[contains(@data, "youtube")]]
  @remove: //object[contains(@data, "youtube")]
}

# REPLACE BODY !!!
body: //article
# REPLACE BODY !!!

title: //meta[@property="og:title"]/@content
description: //meta[@name="description"]/@content
cover: $body//figure
cover: $body//img
image_url: //meta[@property="og:image"]/@content
image_url: $body//img/@src
site_name: //meta[@property="site_name"]/@content
author: //*[has-class("author")]/text()

@datetime(0): //time/@datetime
published_date

#=====LET ME DIE=====
#Original code/idea by Misho @mushi_f
#With modifications by Andrei Afanasiev

$problems: $body
@while( $problems ) {
  $test: $body//*[self::blockquote or self::aside or self::figure or (self::img and not(./ancestor::figure)) or self::iframe or self::slideshow or self::related or self::footer or self::table[not(ancestor::table) and not(descendant::table)]]
 
  $problems: $test/self::*[ancestor::anchor or ancestor::blockquote or ancestor::aside or ancestor::footer or ancestor::strong or ancestor::b or ancestor::em or ancestor::i or ancestor::ins or ancestor::u or ancestor::del or ancestor::s or ancestor::strike or ancestor::code or ancestor::kbd or ancestor::samp or ancestor::tt or ancestor::mark or ancestor::sup or ancestor::pic or ancestor::sub or ancestor::a or ancestor::reference or ancestor::h1 or ancestor::h2 or ancestor::h3 or ancestor::h4 or ancestor::h5 or ancestor::h6 or ancestor::p or ancestor::pre or ancestor::hr or ancestor::img or ancestor::br]
 
  @if( $problems ) {
    $parent: $problems/parent::*
    @detach: $parent/node()
    @set_attr("merge", $index): $@
    <remove>: $parent
    @after_el("./parent::*"): $problems
    @combine: $body//self::*[prev-sibling::*/@merge = @merge]
  }
}
#====================
